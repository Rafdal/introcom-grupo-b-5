*************************************************************************
*	MAIN TESTING			maintest.msa			*
*************************************************************************
	p68h11
*********************************************************************************
*	DEPENDENCIAS: 								*
*		- UI_v2.msa: 	initdis,clrdis,println,waitkey,outchar,numForm	*
*		- string.msa: 	int4str,str2int					*
*		- math_ops.msa:	op_suma,op_rest,op_div,op_mul			*
*		- utils.msa:	delay						*
*										*
*		- str_hand.def (string handlers)				*
*		- op_hand.def (operation handlers)				*
*		- flowctrl.def (flow control macros)				*
* P/ LINKEAR:									*
* 	link11_2 maintest,UI_v2,string,math_ops,utils				*
*********************************************************************************
	extern	initdis,clrdis,println,waitkey,outchar,numForm
	extern	int4str,str2int
	extern	op_suma,op_rest,op_div,op_mul
	extern	delay

	extern	debug,initdeb	(D E B U G )


*********************************************************
*		INCLUSION DE MACROS			*
*********************************************************

$str_hand.def		Incluyo los handlers para string.msa
$op_hand.def		Incluyo los handlers para math_ops.msa

$flowctrl.def		Incluyo las macros para controlar el flujo del programa





	public	main
	public	str_var,op1,lastop	para trackear las vars globales del programa

	extern	ercount,counter	(D E B U G) vars. globales para depurar errores

	rseg	CODE
*****************************************************************
*			MAIN					*
*****************************************************************
main	EQU	*
	lds	#stk_ini
	jsr	setup			ejecuto la configuracion inicial



loop	equ	*
	inc	counter	(D E B U G) increment loop counter

*********************************************************
*		TO-DO	REVISAR ! MODIFICAR !
* Esta parte del codigo esta enmendada, deberia ir
* en numForm, pero a falta de tiempo queda aqui
*********************************************************
	clr	str_var+2		borro los ultimos dos caracteres del display
	clr	str_var+3
	ldaa	lastop			cargo la ultima operacion
	cmpa	#5			si es un (=) [F] code=5
	bne	next01
	ldy	#str_var		convierto el resultado numerico a str
	%int4str op1,str_var,pass0
	jsr	error			si por algun motivo falla, voy a error
pass0	%println str_var		lo muestro por pantalla

next01	equ	*			si no es un igual, evaluo el input
	
	ldy	#str_var		cargo la direccion de la cadena usada por el display
	jsr	numForm			formulario de entrada (AccA = code, AccB = inputVal)

	staa	thisop			guardo el codigo de operacion actual


*********** SWITCH SEGUN LAST OP ******************************************
	ldaa	lastop			Cargo el codigo de ultima operacion
	%switch	OP,0,5			Switch case segun la ultima operacion
	bra	loop			si no se reconoce el codigo, repito

OP0000	equ	*			SUMA (+) A
	%op_hand op_suma,op1,op_err,op_ok		op1 = op1 + B

OP0001	equ	*			RESTA (-) B
	%op_hand op_rest,op1,op_err,op_ok		op1 = op1 - B
	
OP0002	equ	*			CLEAR (C) C
	jsr	setup		ejecuto el setup para cargar todo
	bra	endloop		desde cero y reinicio el loop

OP0003	equ	*			DIVIDE (/) D
	%op_hand op_div,op1,op_err,op_ok		op1 = op1 / B

OP0004	equ	*			MULTIPLICA (*) E
	%op_hand op_mul,op1,op_err,op_ok		op1 = op1 * B

OP0005	equ	*			IGUAL (=) F
	bra	pass					op1 = op1 -> (print result)


*********************************************************
* 		OPERATION ERROR				*
* Si la operacion falla, muestra error y termina	*
op_err	equ	*	
	jsr	error		muestro mensaje de error
	bra	endloop		RETORNO


*********************************************************
* 		OPERATION OK				*
* Si la operacion es exitosa, guardo el resultado	*
op_ok	equ	*			operacion EXITOSA
	tstb				Si B > 0 -> hay resto (solo op_div puede dejar AccB > 0)
	beq	pass
* TO-DO: Implementar mostrar resto de la division


pass	equ	*			;
	ldaa	thisop
	staa	lastop			actualizo el codigo de operacion
	bra	endloop


*---------------------------------------------------------------------------
* Arrastrar por el cógido para detener el programa luego de N repeticiones:
	jsr	debug	E R R O R   T E S T I N G
ERRORS	EQU	250
*---------------------------------------------------------------------------
*********************************************************
* 		END OF LOOP				*
*********************************************************
endloop	equ	*
*	[end functions]
*	...
	jmp	loop			repetir loop







*********************************************************
*		SUBRUTINA SETUP				*
* Descripcion:						*
*	Realiza todas las configuraciones iniciales	*
*********************************************************
setup	EQU	*
	jsr	initdis		inicio el display y teclado
	jsr	clrdis		borro el display
	ldx	#0
	stx	op1		inicio op1 en cero
	stx	str_var+2	borro los ultimos caracteres de str_var
	



	ldaa	#ERRORS
	jsr	initdeb ( D E B U G) inicio las funciones de depuracion
	
*				Inicio el status code del ultimo operador en 0 (+) para
* ldaa	#5
* staa	lastop
* staa	thisop
	clr	lastop		sumar el primer valor con 0 (cero), de esta forma lo guardo
	clr	thisop
	rts


*********************************************************
*		SUBRUTINA ERROR CASE			*
* Descripcion:						*
*	Ejecuta las funciones en caso de error		*
*********************************************************
* TODO: Implementar errores particulares para div,mul,res
*********************************************************
error	EQU	*
	%println err_msg	;muestro mensaje de error
pressC	jsr	waitkey		espero a que presionen cualquier tecla
	cmpa	#$C		si no presionaron CLEAR repito
	bne	pressC

	jsr	setup		inicio todo desde cero
	rts




	rseg	VARS
*********************************************************
*	VARIABLES Y CONSTANTES GLOBALES EN MEMORIA	*
*********************************************************
str_var	FCC	'    '		reservo e inicio la cadena en cero (equivale a un RMB y FCC juntos)
err_msg	FCC	'ERR '		mensaje de error

op1	RMB	2		variable para el operando 1 de 16 bits
lastop	RMB	1		last operation code
thisop	RMB	1		current operation code


*********************************************************
*		STACK DATA				*
*********************************************************
stack	RMB	100
stk_ini	EQU	*-1


	END